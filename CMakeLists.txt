CMAKE_MINIMUM_REQUIRED( VERSION 2.8.11 )
PROJECT( QtOSG )

SET( CMAKE_CXX_FLAGS "-std=c++11 -Wall -pedantic -Wno-long-long" )

SET( CMAKE_INCLUDE_CURRENT_DIR ON )
SET( CMAKE_AUTOMOC ON )

FIND_PACKAGE( Qt5Core )
FIND_PACKAGE( Qt5Gui  )
FIND_PACKAGE( Qt5OpenGL )
FIND_PACKAGE( OpenSceneGraph REQUIRED COMPONENTS osgDB osgGA osgUtil osgViewer )

INCLUDE_DIRECTORIES( ${OPENSCENEGRAPH_INCLUDE_DIRS} )

SET( WITH_PICK_HANDLER         ON CACHE BOOL "Build with pick handler support" )
SET( WITH_SELECTION_PROCESSING ON CACHE BOOL "Build with selection processing support" )

SET( SOURCES
  MainWindow.cpp
  OSGWidget.cpp
  PickHandler.cpp
  QtOSG.cpp
)

ADD_EXECUTABLE( qtosg ${SOURCES} )

IF( WITH_PICK_HANDLER )
  MESSAGE( STATUS "Building with pick handler support" )

  SET_PROPERTY(
    TARGET qtosg
    APPEND PROPERTY COMPILE_DEFINITIONS WITH_PICK_HANDLER
  )
ENDIF()

IF( WITH_SELECTION_PROCESSING )
  MESSAGE( STATUS "Building with selection processing support" )

  SET_PROPERTY(
    TARGET qtosg
    APPEND PROPERTY COMPILE_DEFINITIONS WITH_SELECTION_PROCESSING
  )
ENDIF()

TARGET_LINK_LIBRARIES( qtosg
  ${OPENSCENEGRAPH_LIBRARIES}
  Qt5::Core
  Qt5::Gui
  Qt5::OpenGL
)
